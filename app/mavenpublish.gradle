apply plugin: 'maven-publish'

//生成文档注释
task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}
//将文档打包成jar
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}
//将源码打包
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}


publishing {
    publications {
        dev(MavenPublication) {//dev是我取的名称，可以自定义
            groupId = 'com.github.qitiandear'//域名反写
            artifactId = 'library'//aar的名称
            version = '1.0.1'//版本号
            artifact(androidSourcesJar)//将源码打包进aar，如果不需要可以去掉
            artifact(androidJavadocsJar)//将注释打包进aar，如果不需要可以去掉
            artifact "build/outputs/aar/app-release.aar"//aar包的目录
            pom.withXml{
                def dependenciesNode = asNode().appendNode("dependencies")
                configurations.implementation.allDependencies.forEach(){
                    Dependency dependency ->
                        if (dependency.version != "unspecified" && dependency.name != "unspecified"){
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dependency.group)
                            dependencyNode.appendNode('artifactId', dependency.name)
                            dependencyNode.appendNode('version', dependency.version)
                        }
                }
            }
        }
    }
    repositories {
        maven {
            //maven仓库的地址
//            url = "http://192.168.1.88:8081/repository/maven/"
            //仓库的用户名及密码
//            credentials {
//                username 'admin'
//                password 'admin'
//            }
            //没有maven仓库也可以使用本地仓库
            url = "file://d:/mavenrepository"
        }
    }
}